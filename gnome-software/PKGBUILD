# Maintainer: Dennis ten Hoove <contact@arkanelinux.org>

pkgbase=gnome-software
pkgname=(gnome-software gnome-software-packagekit-plugin)
pkgver=46.0
pkgrel=1
pkgdesc="GNOME Software Tools"
url="https://wiki.gnome.org/Apps/Software/"
arch=(x86_64)
license=(GPL)
depends=(
  appstream
  gsettings-desktop-schemas
  gtk4
  libadwaita
  libsoup3
  libsysprof-capture
  libxmlb
  polkit
	libpackagekit-glib
	python-packaging
)
makedepends=(
  docbook-xsl
  flatpak
  fwupd
  git
  gobject-introspection
  gtk-doc
  libglib-testing
  malcontent
  meson
)
optdepends=(
  'flatpak: Flatpak support plugin'
  'fwupd: fwupd support plugin'
  'malcontent: Parental control plugin'
)
groups=(gnome)
_commit=9dec134914268d135a6f5251ddd624fef2de3c10
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-software.git#commit=$_commit"
  "git+https://gitlab.gnome.org/mwleeds/gnome-pwa-list.git"
)
b2sums=('SKIP'
        'SKIP')

pkgver() {
  cd $pkgbase
	git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase

  git submodule init
  git submodule set-url subprojects/gnome-pwa-list "$srcdir/gnome-pwa-list"
  git -c protocol.file.allow=always submodule update
}



build() {
  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

# Not running tests - need root and a D-Bus system bus

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gnome-software() {
  groups=(gnome)
  optdepends=(
    'flatpak: Flatpak support plugin'
    'fwupd: fwupd support plugin'
    'malcontent: Parental control plugin'
  )

  meson install -C build --destdir "$pkgdir"

  local pkglibdir="$pkgdir/usr/lib/gnome-software"
  _pick packagekit-plugin "$pkglibdir"/plugins-*/libgs_plugin_packagekit*.so
}

package_gnome-software-packagekit-plugin() {
  pkgdesc="PackageKit support plugin for GNOME Software"
  depends=(
    archlinux-appstream-data
    gnome-software
    packagekit
  )

  mv packagekit-plugin/* "$pkgdir"
}
