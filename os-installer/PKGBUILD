# Maintainer: Dennis ten Hoove <contact@arkanelinux.org>
pkgname=os-installer
pkgver=0.3.1
pkgrel=4
pkgdesc='Operating system installer by the GNOME project'
url='https://gitlab.gnome.org/p3732/os-installer'
source=("git+https://gitlab.gnome.org/p3732/$pkgname.git#tag=$pkgver"
        'list_disk_if_zero.patch')
sha256sums=('SKIP' '726189d3b61233c9ec6d75522c99f9b14f47a56674150d2fcb7029e265df42c9')
arch=('x86_64')
license=('GPL3')
makedepends=('meson' 'appstream-glib' 'blueprint-compiler')
depends=('gnome-desktop' 'gtk4' 'libadwaita' 'libgweather-4' 'python-yaml' 'udisks2' 'vte4' 'python-gobject')
optdepends=('epiphany: online assistance'
            'gnome-disk-utility: manual disk partitioning'
            'gnome-control-center: Wi-Fi setup')

prepare () {
	cd $srcdir/$pkgname
	patch -p1 < $srcdir/list_disk_if_zero.patch
}

build () {
	cd $srcdir/$pkgname
	meson setup --prefix=/usr build
}

package() {
	cd $srcdir/$pkgname
	meson install -C build --destdir $pkgdir
}
